-- <?xml version = '1.0' encoding = 'UTF-8'?>
-- <procedure xmlns="http://xmlns.oracle.com/jdeveloper/1112/offlinedb">
--   <properties>
--     <entry>
--       <key>OfflineDBConstants.IMPORT_SOURCE_CONNECTION</key>
--       <value class="java.lang.String">3DcityDB</value>
--     </entry>
--     <entry>
--       <key>OfflineDBConstants.IMPORT_SOURCE_ID</key>
--       <value class="oracle.javatools.db.ReferenceID">
--         <name>REMOVEPAFROMCMABDY</name>
--         <identifier class="java.math.BigDecimal">114504</identifier>
--         <schemaName>YAO</schemaName>
--         <type>PROCEDURE</type>
--       </value>
--     </entry>
--   </properties>
-- </procedure>

CREATE OR REPLACE
PROCEDURE RemovePAfromCMABdy(
  cityModelAspectId NUMBER,
  planningAlternativeId NUMBER,
  outStatus OUT NUMBER,
  outMessage OUT VARCHAR2
)

IS
  -- lokale Variablen
  cityModelAspectWorkspace VARCHAR2(30);
  status NUMBER;
  message VARCHAR2(256);
  memberCount NUMBER;
  cmaCount NUMBER;
  paCount NUMBER;
  paStatus NUMBER;
  cmaCountException EXCEPTION;
  paCountException EXCEPTION;
  removeLastException EXCEPTION;

BEGIN
  -- Abragen der Existenz und des Status der Planungsalternative
  checkPlanningAlternative(planningAlternativeId, paCount, paStatus);
  -- Abragen der Existenz und des Status der Planung
  checkCityModelAspect(cityModelAspectId, cmaCount);

  IF cmaCount = 1 THEN   --  CityModelAspect existiert
    IF paCount = 1 THEN   -- Planungsalternative existiert

      -- Zählen der Planungsalternativen des CityModelAspects
      SELECT COUNT(city_model_aspect_id) INTO memberCount
      FROM city_model_aspect_component
      WHERE city_model_aspect_id = cityModelAspectId;

      IF memberCount > 1 THEN   -- es bleibt mind. eine Planungsalternative
        -- Löschen des entsprechenden Tupels der Verknüpfungstabelle
        DELETE city_model_aspect_component
        WHERE city_model_aspect_id = cityModelAspectId AND planning_alternative_id = planningAlternativeId;

        COMMIT;
        setOutParameter(1, NULL, outStatus, outMessage);

      ELSE   -- die letzte Planungsalternative soll entfernt werden => CMA löschen
        DeleteCityModelAspectBdy(cityModelAspectId, status, message);
        IF status = 1 THEN   -- fehlerfrei gelöscht
          COMMIT;
          setOutParameter(2, NULL, outStatus, outMessage);
        ELSE   -- Fehler beim Löschen
          setOutParameter(0, message, outStatus, outMessage);
        END IF;

      END IF;

    ELSE   -- Planungsalternative existiert nicht
      RAISE paCountException;
    END IF;

  ELSE   -- CityModelAspect existiert nicht
    RAISE cmaCountException;
  END IF;


EXCEPTION
  WHEN paCountException THEN
    setOutParameter(0, '3D-Geo-DB: Planungsalternative existiert nicht', outStatus, outMessage);
  WHEN cmaCountException THEN
    setOutParameter(0, '3D-Geo-DB: CityModelAspect existiert nicht', outStatus, outMessage);
  WHEN OTHERS THEN
    setOutParameter(0, sqlerrm, outStatus, outMessage);

END;
/
