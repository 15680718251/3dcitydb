-- <?xml version = '1.0' encoding = 'UTF-8'?>
-- <procedure xmlns="http://xmlns.oracle.com/jdeveloper/1112/offlinedb">
--   <properties>
--     <entry>
--       <key>OfflineDBConstants.IMPORT_SOURCE_CONNECTION</key>
--       <value class="java.lang.String">3DcityDB</value>
--     </entry>
--     <entry>
--       <key>OfflineDBConstants.IMPORT_SOURCE_ID</key>
--       <value class="oracle.javatools.db.ReferenceID">
--         <name>ADDPATOCMABDY</name>
--         <identifier class="java.math.BigDecimal">114503</identifier>
--         <schemaName>YAO</schemaName>
--         <type>PROCEDURE</type>
--       </value>
--     </entry>
--   </properties>
-- </procedure>

CREATE OR REPLACE
PROCEDURE AddPAtoCMABdy(
  cityModelAspectId NUMBER,
  planningAlternativeId NUMBER,
  outStatus OUT NUMBER,
  outMessage OUT VARCHAR2
)

IS
  -- lokale Variablen
  planningId NUMBER;
  numb NUMBER;
  cmaCount NUMBER;
  paCount NUMBER;
  paStatus NUMBER;
  planningException EXCEPTION;
  cmaCountException EXCEPTION;
  paCountException EXCEPTION;

BEGIN
  -- Abragen der Existenz und des Status der Planungsalternative
  checkPlanningAlternative(planningAlternativeId, paCount, paStatus);
  -- Abragen der Existenz und des Status der Planung
  checkCityModelAspect(cityModelAspectId, cmaCount);

  IF cmaCount = 1 THEN   --  CityModelAspect existiert
    IF paCount = 1 THEN   -- Planungsalternative existiert

      -- Suchen der zugehörigen Planung der Planungsalternative
      SELECT planning_id INTO planningId
      FROM planning_alternative
      WHERE id = planningAlternativeId;

      -- Prüfen wieviele Alternativen aus der entsprechenden Planung
      -- dem CityModelAspect bereits zugeordnet sind
      SELECT COUNT(c.planning_alternative_id) INTO numb
      FROM city_model_aspect_component c, planning_alternative p
      WHERE
        c.city_model_aspect_id = cityModelAspectId AND
        c.planning_alternative_id = p.id AND
        p.planning_id = planningId;

      IF numb < 1 THEN   -- noch keine Planungsalternative dieser Planung im CMA
        -- Eintrag in Zuordnungstabelle
        INSERT INTO city_model_aspect_component
        VALUES (cityModelAspectId, planningAlternativeId);

        COMMIT;
        setOutParameter(1, NULL, outStatus, outMessage);

      ELSE   -- es ist bereits eine Planungsalternative dieser Planung zugeordnet
        RAISE planningException;
      END IF;

    ELSE   -- Planungsalternative existiert nicht
      RAISE paCountException;
    END IF;

  ELSE   -- CityModelAspect existiert nicht
    RAISE cmaCountException;
  END IF;


EXCEPTION
  WHEN planningException THEN
    setOutParameter(0, '3D-Geo-DB: CityModelAspect enthält bereits eine Planungsalternative dieser Planung', outStatus, outMessage);
  WHEN paCountException THEN
    setOutParameter(0, '3D-Geo-DB: Planungsalternative existiert nicht', outStatus, outMessage);
  WHEN cmaCountException THEN
    setOutParameter(0, '3D-Geo-DB: CityModelAspect existiert nicht', outStatus, outMessage);
  WHEN OTHERS THEN
    setOutParameter(0, sqlerrm, outStatus, outMessage);

END;
/
